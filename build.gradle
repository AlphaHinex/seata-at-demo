buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'https://maven.pkg.github.com/AlphaHinex/spring-roll'
            credentials(PasswordCredentials) {
                username = project.findProperty("gpr.user") ?: System.getenv("GH_OWNER")
                password = project.findProperty("gpr.reader") ?: System.getenv("GH_PKG_READER")
            }
        }
    }
    dependencies {
        classpath 'io.github.spring-roll:roll-dev-configs:0.0.3-SNAPSHOT'
    }
}

plugins {
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'org.springframework.boot' version '2.2.1.RELEASE'
    id "io.freefair.lombok" version "4.1.6"
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url 'https://maven.pkg.github.com/AlphaHinex/spring-roll'
            credentials(PasswordCredentials) {
                username = project.findProperty("gpr.user") ?: System.getenv("GH_OWNER")
                password = project.findProperty("gpr.reader") ?: System.getenv("GH_PKG_READER")
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'utf-8'
        options.compilerArgs = [
            '-Xlint:-options',
            '-Xlint:deprecation',
            '-Xlint:unchecked',
            '-Werror'
        ]
    }

    tasks.withType(GroovyCompile) {
        options.encoding = 'utf-8'
        options.compilerArgs = ['-Xlint:-options']
        groovyOptions.encoding = 'utf-8'
    }
}

apply plugin: 'idea'
apply from: this.getClass().getClassLoader().getResource("META-INF/configs/scaffold/module.gradle")

group = 'io.github.alphahinex.demo.seata.at'
version = '0.0.1-SNAPSHOT'

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'groovy'

    apply plugin: 'io.spring.dependency-management'
    apply plugin: "io.freefair.lombok"

    apply from: this.getClass().getClassLoader().getResource("META-INF/configs/dependencies.gradle")

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot:${versions.spring_boot}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR1"
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = rootProject.group
    version = rootProject.version

    dependencies {
        annotationProcessor libraries.lombok,
                            libraries.spring_boot_configuration_processor

                compileOnly libraries.lombok,
                            libraries.spring_boot_starter_logging

        implementation libraries.spring_boot_starter_data_jpa
        implementation libraries.mysql_driver
        implementation libraries.druid_spring_boot_starter
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        implementation 'com.alibaba.cloud:spring-cloud-alibaba-nacos-discovery:2.1.1.RELEASE'

        implementation libraries.roll_web

        testImplementation libraries.spring_boot_starter_test
        testImplementation libraries.h2
        testImplementation libraries.roll_test
    }

}