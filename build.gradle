buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url 'https://maven.pkg.github.com/AlphaHinex/spring-roll'
            credentials(PasswordCredentials) {
                username = project.findProperty("gpr.user") ?: System.getenv("GH_OWNER")
                password = project.findProperty("gpr.reader") ?: System.getenv("GH_PKG_READER")
            }
        }
    }
    dependencies {
        classpath 'io.github.spring-roll:roll-dev-configs:0.0.4.RELEASE'
    }
}

plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id "io.freefair.lombok" version "4.1.6"
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url 'https://maven.pkg.github.com/AlphaHinex/spring-roll'
            credentials(PasswordCredentials) {
                username = project.findProperty("gpr.user") ?: System.getenv("GH_OWNER")
                password = project.findProperty("gpr.reader") ?: System.getenv("GH_PKG_READER")
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'utf-8'
        options.compilerArgs = [
            '-Xlint:-options',
            '-Xlint:deprecation',
            '-Xlint:unchecked',
            '-Werror'
        ]
    }

    tasks.withType(GroovyCompile) {
        options.encoding = 'utf-8'
        options.compilerArgs = ['-Xlint:-options']
        groovyOptions.encoding = 'utf-8'
    }
}

apply plugin: 'idea'
apply from: this.getClass().getClassLoader().getResource("META-INF/configs/scaffold/module.gradle")

group = 'io.github.alphahinex.demo.seata.at'
version = '0.0.1-SNAPSHOT'

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'groovy'

    apply plugin: "io.freefair.lombok"

    apply from: this.getClass().getClassLoader().getResource("META-INF/configs/dependencies.gradle")

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = rootProject.group
    version = rootProject.version

    dependencies {
        annotationProcessor platform("org.springframework.boot:spring-boot-dependencies:${versions.spring_boot}")
             implementation platform("org.springframework.boot:spring-boot-dependencies:${versions.spring_boot}")
             implementation platform("org.springframework.cloud:spring-cloud-dependencies:${versions.spring_cloud}")
             implementation platform("com.alibaba.cloud:spring-cloud-alibaba-dependencies:${versions.spring_cloud_alibaba}")

        annotationProcessor libraries.lombok,
                            libraries.spring_boot_configuration_processor

                compileOnly libraries.lombok,
                            libraries.spring_boot_starter_logging

             implementation libraries.spring_boot_starter_data_jpa
             implementation libraries.mysql_driver
             implementation libraries.spring_cloud_starter_openfeign
             implementation libraries.spring_cloud_alibaba_nacos_discovery

             implementation libraries.roll_web
             implementation libraries.roll_base_jpa

         testImplementation libraries.spring_boot_starter_test
         testImplementation libraries.h2
         testImplementation libraries.roll_test
    }

}